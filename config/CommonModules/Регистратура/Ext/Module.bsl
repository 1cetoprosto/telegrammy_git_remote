
Процедура УдалениеНеЗаконченныхЗаписей() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписьНаПрием.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаписьНаПрием КАК ЗаписьНаПрием
		|ГДЕ
		|	ЗаписьНаПрием.Проведен = ЛОЖЬ
		|	И ЗаписьНаПрием.Дата < &ДатаВремя";
	
	Запрос.УстановитьПараметр("ДатаВремя", ТекущаяДата() - Константы.ВремяУдаленияЗаписей.Получить()*60);
	                                                    
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура НапоминаниеОЗаписи(Интервал) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписиНаПрием.Мастер.Бот.Ссылка КАК МастерБотСсылка,
		|	ЗаписиНаПрием.Мастер.Бот.Обработка КАК МастерБотОбработка,
		|	ЗаписиНаПрием.Контрагент.Идентификатор КАК КонтрагентИдентификатор,
		|	ЗаписиНаПрием.ДатаВремяЗаписи КАК ДатаВремяЗаписи,
		|	ЗаписиНаПрием.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ЗаписиНаПрием КАК ЗаписиНаПрием
		|ГДЕ
		|	ЗаписиНаПрием.ТипЗаписиНаПрием = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейНаПрием.Телеграм)
		|	И &УсловиеВремени
		|ИТОГИ ПО
		|	МастерБотСсылка,
		|	КонтрагентИдентификатор,
		|	ДатаВремяЗаписи";
	
	Запрос.УстановитьПараметр("ДатаВремя", ТекущаяДата());
	
	УсловиеВремени = ?(Интервал="Час", "ДОБАВИТЬКДАТЕ(ЗаписиНаПрием.ДатаВремяЗаписи, МИНУТА, -ЗаписиНаПрием.Мастер.ВремяНапоминанияОЗаписиЧасовое) < &ДатаВремя",
	"ДОБАВИТЬКДАТЕ(ЗаписиНаПрием.ДатаВремяЗаписи, МИНУТА, -ЗаписиНаПрием.Мастер.ВремяНапоминанияОЗаписиСуточное) < &ДатаВремя");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеВремени", УсловиеВремени);  
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаМастерБотСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаМастерБотСсылка.Следующий() Цикл
		// Создать обработку Бота
		АПИТелеграм = Обработки.Телеграм.Создать();
		АПИТелеграм.Бот = ВыборкаМастерБотСсылка.МастерБотСсылка;
		
		ДвижокБота = Обработки[ВыборкаМастерБотСсылка.МастерБотОбработка].Создать();
		ДвижокБота.Телеграм = АПИТелеграм;
		
		ВыборкаКонтрагентИдентификатор = ВыборкаМастерБотСсылка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаКонтрагентИдентификатор.Следующий() Цикл
			ИДКонтрагента = ВыборкаКонтрагентИдентификатор.КонтрагентИдентификатор;
			
			ВыборкаДатаВремяЗаписи = ВыборкаКонтрагентИдентификатор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ТекстСообщения = "";
			
			Пока ВыборкаДатаВремяЗаписи.Следующий() Цикл
				
				ВремяЗаписи = ВыборкаДатаВремяЗаписи.ДатаВремяЗаписи;
				ТекстСообщения = "Нагадую про запис на " + ВремяЗаписи +":"+  Символы.ПС;
				
				ВыборкаДетальныеЗаписи = ВыборкаДатаВремяЗаписи.Выбрать();
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
					ТекстСообщения = ТекстСообщения + Номенклатура + Символы.ПС;
				КонецЦикла;
				
			КонецЦикла;
			
			ДвижокБота.ОповеститьКонтрагента(ИДКонтрагента, ТекстСообщения);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НапоминаниеОЗаписиЧасовое() Экспорт
	НапоминаниеОЗаписи("Час");
КонецПроцедуры

Процедура НапоминаниеОЗаписиСуточное() Экспорт
	НапоминаниеОЗаписи("Сутки");
КонецПроцедуры

Процедура УдалениеНеЗаконченныхСессийКонтрагентов() Экспорт
	
	Продолжать = Истина;
	
	Пока Продолжать Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СессииКонтрагентов.Бот КАК Бот
		|ИЗ
		|	РегистрСведений.СессииКонтрагентов КАК СессииКонтрагентов
		|ГДЕ
		|	СессииКонтрагентов.ДатаСоздания < &ДатаВремя";
		
		Запрос.УстановитьПараметр("ДатаВремя", ТекущаяДата() - 60*60*24);
		
		Продолжать = Не Запрос.Выполнить().Пустой();
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.СессииКонтрагентов.СоздатьНаборЗаписей();
			
			НаборЗаписей.Отбор.Бот.Установить(Выборка.Бот);
			
			НаборЗаписей.Записать(Истина);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры
